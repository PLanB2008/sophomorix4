#!/usr/bin/perl -w
# $Id$
# Dieses Script (sophomorix-print) wurde von Rüdiger Beck erstellt
# Es ist freie Software
# Bei Fehlern wenden Sie sich bitte an mich.
# jeffbeck@web.de  oder  jeffbeck@linuxmuster.net

# Libraries
use strict;
use Getopt::Long;
use Sophomorix::SophomorixConfig;
use Sophomorix::SophomorixBase qw(
                                 print_line
                                 print_title
                                 time_stamp_AD
                                 time_stamp_file
                                 ymdhms_to_epoch
                                 ymdhms_to_date
                                 unlock_sophomorix
                                 lock_sophomorix
                                 log_script_start
                                 log_script_end
                                 log_script_exit
                                 backup_auk_file
                                 get_passwd_charlist
                                 get_plain_password
                                 check_options
                                 config_sophomorix_read
                                 result_sophomorix_init
                                 result_sophomorix_add
                                 result_sophomorix_add_summary
                                 result_sophomorix_check_exit
                                 result_sophomorix_print
                                 filelist_fetch
                                 remove_whitespace
                                 json_dump
                                 recode_utf8_to_ascii
                                 );
use Sophomorix::SophomorixSambaAD qw(
                                 AD_school_create
                                 AD_bind_admin
                                 AD_unbind_admin
                                 AD_user_create
                                 AD_group_create
                                 AD_group_addmember
                                 AD_group_update
                                 AD_get_schoolname
                                 AD_get_name_tokened
                                 AD_dn_fetch_multivalue
                                 AD_get_AD
                                 AD_get_user
                                 AD_get_print_data
                                 AD_dns_get
                                 AD_get_passwd
                                 AD_object_search
                                 get_forbidden_logins
                                    );

Getopt::Long::Configure ("bundling");
use Net::LDAP;
use Sys::Hostname;
use JSON;
use Data::Dumper;

my @arguments = @ARGV;

my $dev_null="1>/dev/null 2>/dev/null";
my $json=0;

$Conf::log_level=1;
if($Conf::log_level>=3){
   # avoid logging STDOUT,STDERR to /dev/null
   $dev_null="";
}
my $help=0;
my $info=0;
my $caller="";
my $caller_copy="";

my $back_in_time=0;

my $command="latex";
my $user="";
my $class="";


my $project="";
my $classlist="";


my $one_per_page=0;
my $skiplock=0;

my $template="";

my $school="";

# Parsen der Optionen
my $testopt=GetOptions(
           "verbose|v+" => \$Conf::log_level,
           "school|s=s" => \$school,
           "class|c=s" => \$class,
           "project|p=s" => \$project,
           "template=s" => \$template,
           "user|u=s" => \$user,
           "classlist=s" => \$classlist,
           "help|h" => \$help,
           "caller=s" => \$caller,
           "caller-copy|callercopy=s" => \$caller_copy,
           "command=s" => \$command,
           "back-in-time|backintime=i" => \$back_in_time,
           "one-per-page" => \$one_per_page,
           "skiplock" => \$skiplock,
           "info|i" => \$info
          );

if ($school eq ""){
    $school=$DevelConf::name_default_school;
}

my %sophomorix_result=&result_sophomorix_init("sophomorix-check");
# Prüfen, ob Optionen erkannt wurden, sonst Abbruch
&check_options($testopt,\%sophomorix_result,$json);

# Reading Configuration
my ($ldap,$root_dse) = &AD_bind_admin(\@arguments,\%sophomorix_result,$json);
my $root_dns=&AD_dns_get($root_dse);
my %sophomorix_config=&config_sophomorix_read($ldap,$root_dse,\%sophomorix_result);
my ($smb_admin_pass)=&AD_get_passwd($DevelConf::sophomorix_file_admin,
                                     $DevelConf::secret_file_sophomorix_file_admin);


# --help
if ($help==1) {
   # calculate scriptname
   my @list = split(/\//,$0);
   my $scriptname = pop @list;
   print('
sophomorix-print prints user account data of ONE school to 
  /var/lib/sophomorix/print-data

Options:
  -h  / --help
  -v  / --verbose
  -vv / --verbose --verbose

sophomorix-print looks only on one school for users (without option the default-school)
For other schools use the following option:
  -s <schoolname> / --school <schoolname>

The user given by option --caller/--caller-copy must exist!

Choose the way of pdf generation (Default: latex->dvips->ps2pdf)
  --command latex  (This is the default: latex->dvips->ps2pdf)
  --command pdflatex

Examples to print a class:
  sophomorix-print --class <prefix>-<class> --caller bz [--school <schoolname>]
  sophomorix-print --class <prefix>-<class> --caller-copy bz [--school <schoolname>]

Examples to print timely additions:
  sophomorix-print -i  (shows values and dates for <num>)
  sophomorix-print --back-in-time <num> --caller bz [--school <schoolname>]
  sophomorix-print --back-in-time <num> --caller-copy bz [--school <schoolname>]


Deprecated in sophomorix4:
  -p project / --project project
  --one-per-page

Please see the sophomorix-print(8) man pages for full documentation
');
   print "\n";
   exit;
}

&log_script_start(@arguments);

# --info
if ($info==0){
    &print_title("sophomorix-print is creating output ...");
}

system("mkdir -p $sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}");


# ===========================================================================
# Calculate output_file_basename and template file
# ===========================================================================
my $output_file_basename="";
my $template_num;
my $template_path_abs=$sophomorix_config{'INI'}{'LATEX'}{'TEMPLATES'}.
                      "/datalist-".
                      $sophomorix_config{'GLOBAL'}{'LANG'}.
                      "-";

my $class_filename_part="";
if ($class ne ""){
    $template_num=36;
    $class_filename_part="$class";
}elsif ($project ne ""){
    $template_num=36;
    $class_filename_part="$project";
}elsif ($classlist ne ""){
    $template_num=36;
    $class_filename_part="$classlist-classlist";
}elsif ($user ne ""){
    $template_num=1;
    $class_filename_part="user";
} else {
    $template_num=36;
    $class_filename_part="add";
}

my $caller_filename_part="-unknown";
# vars filled by --caller or --callercopy
my ($firstname_utf8_AD,$lastname_utf8_AD,$adminclass_AD,$existing_AD,$exammode_AD,$role_AD,
    $home_directory_AD,$user_account_control_AD,$toleration_date_AD,$deactivation_date_AD,
    $school_AD,$status_AD,$firstpassword_AD);

# --caller
if ($caller ne ""){
    ($firstname_utf8_AD,$lastname_utf8_AD,$adminclass_AD,$existing_AD,$exammode_AD,$role_AD,
     $home_directory_AD,$user_account_control_AD,$toleration_date_AD,$deactivation_date_AD,
     $school_AD,$status_AD,$firstpassword_AD)=
     &AD_get_user({ldap=>$ldap,
                   root_dse=>$root_dse,
                   root_dns=>$root_dns,
                   user=>$caller,
                 });
    if ($existing_AD eq "FALSE"){
        print "\nERROR: User $caller does not exist\n\n";
        exit;
    }
    $caller_filename_part="-"."$caller";
}

# --caller-copy
if ($caller_copy ne ""){
    ($firstname_utf8_AD,$lastname_utf8_AD,$adminclass_AD,$existing_AD,$exammode_AD,$role_AD,
     $home_directory_AD,$user_account_control_AD,$toleration_date_AD,$deactivation_date_AD,
     $school_AD,$status_AD,$firstpassword_AD)=
     &AD_get_user({ldap=>$ldap,
                   root_dse=>$root_dse,
                   root_dns=>$root_dns,
                   user=>$caller_copy,
                 });
    if ($existing_AD eq "FALSE"){
        print "\nERROR: User $caller_copy does not exist\n\n";
        exit;
    }
    $caller_filename_part="-"."$caller_copy";
}

# assemble file basename
$output_file_basename="$class_filename_part"."$caller_filename_part";

# --template  /abs/path/override
if ($template ne ""){
    $template_path_abs=$template;
} else {
    # assemble template path
    $template_path_abs=$template_path_abs.$template_num."-template.tex";
}
# test existance of template file
if (not -f $template_path_abs){
    print "\nERROR: Template does not exist: $template_path_abs\n\n";
    exit;
}

&print_title("Basename of output files: $output_file_basename");
&print_title("Template: $template_path_abs");

# ===========================================================================
# Read data from AD
# ===========================================================================
my $ref_AD_print_data=&AD_get_print_data({ldap=>$ldap,
                                      root_dse=>$root_dse,
                                      root_dns=>$root_dns,
                                      users=>"TRUE",
                                      school=>$school,
                                      sophomorix_config=>\%sophomorix_config,
             });
#print Dumper($ref_AD_print_data);



# --info
if ($info==1){
    my @datelist=&sorted_datelist();
    my $back_in_time_count=0;
    print "+---------------------+---------------------+-------+-------------------+\n";
    print "| Option to use       | Date                | users | AD-Date           |\n";
    print "+---------------------+---------------------+-------+-------------------+\n";
    foreach my $ymdhms (@datelist){
        my $date=&ymdhms_to_date($ymdhms);
        my $count=$#{ $ref_AD_print_data->{'LIST_BY_sophomorixCreationDate'}{$ymdhms}}+1;
        printf "  --back-in-time %-6s $date %7s   $ymdhms\n",$back_in_time_count, $count;
        $back_in_time_count++;
    }
    exit;
}


# decide which list to print
my $ref_printlist;
if ($class ne ""){
    # a class is given
    $ref_printlist=$ref_AD_print_data->{'LIST_BY_sophomorixAdminClass'}{$class};
    if (not exists $ref_AD_print_data->{'LOOKUP_BY_sophomorixAdminClass'}{$class}){
        print "\nERROR: Class $class has no printout data in school $school\n\n";
        exit;
    }
    &print_data($class,$output_file_basename,$ref_printlist);
} elsif ($user ne ""){
    # --user <user1><user2>,... are given
    my %printdata=();
    my @users=split(/,/,$user);
    foreach my $username (@users){
        if (exists $ref_AD_print_data->{'LOOKUP_BY_sAMAccountName'}{$username}){
            print "   * $username selected\n";
            push @{ $printdata{'LIST'} }, $ref_AD_print_data->{'LOOKUP_BY_sAMAccountName'}{$username};
        } else {
            print "\nERROR: User $username has no printout data in school $school\n\n";
            exit;
        }
    }
    $ref_printlist=$printdata{'LIST'};
    &print_data("Users",$output_file_basename,$ref_printlist);
} else {
    # go for back in time stuff, if nothing else ist given
    my @datelist=&sorted_datelist();
    my $date=&ymdhms_to_date($datelist[$back_in_time]);
    print " Going Back in time $back_in_time steps to $date ($datelist[$back_in_time])\n";
    $ref_printlist=$ref_AD_print_data->{'LIST_BY_sophomorixCreationDate'}{$datelist[$back_in_time]};
    &print_data($date,$output_file_basename,$ref_printlist);
}


 

# Walk through printlist
#foreach my $item ( @{ $ref_printlist } ){
#    print "$item\n";
#}

# --caller-copy (do the copy stuff)
if ($caller_copy ne ""){
    &print_title("Copying files to user $caller_copy");
    my $smb_dir_home=$home_directory_AD;
    $smb_dir_home=~s/\\/\//g;
    my ($string1,$rel_path_home)=split(/$school_AD/,$smb_dir_home); # to home
    $rel_path_home=$rel_path_home."/".$sophomorix_config{'INI'}{'LATEX'}{'PRINT_HOME_SUBDIR'};
    my $smbclient_command=$sophomorix_config{'INI'}{'EXECUTABLES'}{'SMBCLIENT'}.
                          " -U ".$DevelConf::sophomorix_file_admin."%'".
                          $smb_admin_pass."'".
                          " //$root_dns/$school_AD ".
                          " -c 'md $rel_path_home; cd $rel_path_home; lcd $sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'};".
                          " prompt; mput $output_file_basename*; exit;'";
    print "$smbclient_command\n";
    system($smbclient_command);
}



&AD_unbind_admin($ldap);
&log_script_end(\@arguments,\%sophomorix_result,\%sophomorix_config,$json);




# ===========================================================================
# Subroutinen
# ===========================================================================

sub pdflatex {
    &print_title("pdflatex is creating $output_file_basename.pdf");
    my $pdflatex_command="cd $sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}; ".
	                 "$sophomorix_config{'INI'}{'LATEX'}{'BIN_PDFLATEX'} ".$output_file_basename.".tex ".$dev_null;
    print "  * $pdflatex_command\n";
    system($pdflatex_command);
    system($pdflatex_command); # 2x, to create table of contents correctly

    # clean up
    system("rm $sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}/$output_file_basename.log $dev_null");
    system("rm $sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}/$output_file_basename.toc $dev_null");
    system("rm $sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}/$output_file_basename.aux $dev_null");
    system("rm $sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}/$output_file_basename.out $dev_null");
    # make files ro for root
    system("chmod 400 $sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}/$output_file_basename.* $dev_null");

}



sub latex {
    &print_title("latex is creating $output_file_basename.dvi");
    # PS aus $output_file_basename.tex erzeugen
    my $latex_command="cd $sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}; ".
	              "$sophomorix_config{'INI'}{'LATEX'}{'BIN_LATEX'} ".$output_file_basename.".tex ".$dev_null;
    print "  * $latex_command\n";
    system($latex_command);
    system($latex_command); # 2x, to create table of contents correctly

    &print_title("dvips is creating $output_file_basename.ps");
    my $dvips_command="cd $sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}; ".
	              "$sophomorix_config{'INI'}{'LATEX'}{'BIN_DVIPS'} ".$output_file_basename.".dvi ".$dev_null;
    print "  * $dvips_command\n";
    system($dvips_command);

    &print_title("ps2pdf is creating $output_file_basename.pdf");
    my $ps2pdf_command="cd $sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}; ".
	              "$sophomorix_config{'INI'}{'LATEX'}{'BIN_PS2PDF'} ".$output_file_basename.".ps ".$dev_null;
    print "  * $ps2pdf_command\n";
    system($ps2pdf_command);

    # clean up
    system("rm $sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}/$output_file_basename.log $dev_null");
    system("rm $sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}/$output_file_basename.toc $dev_null");
    system("rm $sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}/$output_file_basename.aux $dev_null");
    system("rm $sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}/$output_file_basename.out $dev_null");
    system("rm $sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}/$output_file_basename.dvi $dev_null");
}



sub sorted_datelist {
    my @datelist=();
    foreach my $date (keys %{$ref_AD_print_data->{'LIST_BY_sophomorixCreationDate'}}) {
	#print "$date\n";
        push @datelist, $date;
    }
    # sort the dates in reverse order
    @datelist=sort{$b cmp $a} @datelist; # for strings
    return @datelist;  
}



sub print_data {
    my ($class,$output_file_basename,$ref_printlist)=@_;
    &open_output_files($output_file_basename);
    &create_latex_from_template({ref_printlist=>$ref_printlist,
                                 ADMINCLASS=>$class,
                                 SCHOOL=>$school,
                                 TEACHERMEMBERS=>"---",
                                 FILENAME=>$output_file_basename,
                                 sophomorix_config=>\%sophomorix_config,
                                 sophomorix_result=>\%sophomorix_result,
                    });
    &create_csv_files($ref_printlist);
    &close_output_files($output_file_basename);
    &build_results; 
    &make_output_files_ro;
}



sub build_results {
    if ($command eq "pdflatex"){
        &pdflatex;
    } elsif ($command eq "latex"){
        # hoehere Qualitaet
        &latex;
    } else {
        print "\nI do not know how to process the file ($command ?)\n\n";
    }
}



sub make_output_files_ro {
    system("chmod 400 $sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}/${output_file_basename}.* $dev_null");
    system("chmod 400 $sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}/${output_file_basename}-* $dev_null");
    system("chmod 400 $sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}/${output_file_basename}_* $dev_null");
}



sub datablock_from_list {
    my ($chead,$ref_printlist) = @_;   
    my @list = @{ $ref_printlist };
    @list = sort @list;
    my $dataline_count=0;
    # header
    my $datablock="\\sophomorixnewpage{".$chead."}{}{}{}{}{}{}{}{}%\n";
    foreach my $item (@list){
        #print "Add to datablock: $item\n";
        $dataline_count++;
        my @data=split(/;/,$item);
        $datablock=$datablock."\\sophomorixdatabox{".$data[0]."}".
                                                 "{".$data[1]."}".
                                                 "{".$data[2]."}".
                                                 "{".$data[3]."}".
                                                 "{".$data[4]."}".
                                                 "{".$data[5]."}".
                                                 "{".$data[8]."}".
                                                 "{".$data[9]."}".
                                                 "{".$data[10]."}%\n";
    }
    until ($dataline_count==36){
        $dataline_count++;
        $datablock=$datablock."\\sophomorixdatabox{}{}{}{}{}{}{}{}{}%\n";
    }
    #print $datablock;
    return $datablock;
}



sub create_csv_files {
    @{ $ref_printlist } = sort @{ $ref_printlist };
    foreach my $item ( @{ $ref_printlist } ){
        my @data=split(/;/,$item);
        my $csv=$data[1]." ".$data[0].";".$data[5].";".$data[2].";".$data[3].";";
        print CSV $csv."\l\r";
        print UNIXCSV $csv."\n";
        my $webuntis=$data[5].";".$data[0].";".$data[1].";".$data[2].";".$data[10].";";
        print WEBUNIXCSV $webuntis."\n";
        print WEBCSV $webuntis."\l\r";
    }
}



sub create_latex_from_template {
    my ($arg_ref) = @_;
    my $ref_printlist = $arg_ref->{ref_printlist};
    my $adminclass = $arg_ref->{ADMINCLASS};
    my $school = $arg_ref->{SCHOOL};
    my $teachermembers = $arg_ref->{TEACHERMEMBERS};
    my $filename = $arg_ref->{FILENAME};
    my $ref_sophomorix_config = $arg_ref->{sophomorix_config};
    my $ref_sophomorix_result = $arg_ref->{sophomorix_result};

    my $datablock=&datablock_from_list($adminclass,$ref_printlist);

    open(TEMPLATE,"<$template_path_abs") || die "Fehler: $!";
    my $datablockmode="FALSE";
    while (<TEMPLATE>){
        my $line=$_;

        # replacements
        $line=~s/\\textcolor\{red\}\{ADMINCLASS\}/$adminclass/;
        my $schoolstring=$ref_sophomorix_config->{'SCHOOLS'}{$school}{'SCHOOL_LONGNAME'}."(".$school.")";
        $line=~s/\\textcolor\{red\}\{SCHOOL\}/$schoolstring/;
        $line=~s/\\textcolor\{red\}\{FILENAME\}/$filename/;

        if ($line=~m/DATABLOCK START/){
            $datablockmode="TRUE";
            print LATEX $line; # print the line to make debugging easier
            print LATEX $datablock;
        } elsif ($line=~m/DATABLOCK END/){
            $datablockmode="FALSE";
        }

        if ($datablockmode eq "FALSE"){
            print LATEX $line;
        }
    }
    close(TEMPLATE);
}



sub open_output_files {
    open(LATEX,">$sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}/${output_file_basename}.tex") || die "Fehler: $!";
    # CSV Windows
    open(CSV,">$sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}/${output_file_basename}.csv") || die "Fehler: $!";
    # CSV Linux
    open(UNIXCSV,">$sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}/${output_file_basename}-unix.csv") || die "Fehler: $!";
    # Webuntis CSV Windows
    open(WEBCSV,">$sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}/${output_file_basename}_WebUntis.csv") || die "Fehler: $!";
    # Webuntis CSV Linux
    open(WEBUNIXCSV,">$sophomorix_config{'INI'}{'LATEX'}{'PRINT_PATH'}/${output_file_basename}_WebUntis-unix.csv") || die "Fehler: $!";
}



sub close_output_files {
    close(LATEX);
    close(CSV);
    close(UNIXCSV);
    close(WEBCSV);
    close(WEBUNIXCSV);
}
