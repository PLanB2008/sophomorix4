#!/usr/bin/perl -w
# This script (sophomorix-extraclass) is maintained by Rüdiger Beck
# It is Free Software (License GPLv3)
# If you find errors, contact the author
# jeffbeck@web.de  or  jeffbeck@linuxmuster.net

# modules
use strict;
use Getopt::Long;
use Time::Local;
Getopt::Long::Configure ("bundling");
use Sophomorix::SophomorixConfig;
use List::MoreUtils qw(uniq);
use Net::LDAP;
use Data::Dumper;
$Data::Dumper::Indent = 1;
$Data::Dumper::Sortkeys = 1;
$Data::Dumper::Useqq = 1;
$Data::Dumper::Terse = 1; 
use JSON;
use Sophomorix::SophomorixBase qw(
                                 print_line
                                 print_title
                                 unlock_sophomorix
                                 lock_sophomorix
                                 log_script_start
                                 log_script_end
                                 log_script_exit
                                 backup_auk_file
                                 get_passwd_charlist
                                 get_plain_password
                                 check_options
                                 config_sophomorix_read
                                 result_sophomorix_init
                                 result_sophomorix_add
                                 result_sophomorix_check_exit
                                 result_sophomorix_print
                                 remove_from_list
                                 json_dump
                                 epoch_to_ymdhms
                                 );
use Sophomorix::SophomorixSambaAD qw(
                                 AD_school_create
                                 AD_bind_admin
                                 AD_dns_get
                                 AD_unbind_admin
                                 AD_user_create
                                 AD_get_user
                                 AD_group_create
                                 AD_group_kill
                                 AD_group_addmember
                                 AD_get_schoolname
                                 AD_get_name_tokened
                                 AD_class_fetch
                                 AD_group_update
                                 AD_project_sync_members
                                 AD_dn_fetch_multivalue
                                 AD_get_passwd
                                 AD_get_groups_v
                                 AD_get_full_groupdata
                                    );
my @arguments = @ARGV;


############################################################
# Option handling
############################################################
my %options=();
# define possible action in a script and what OBJECT they need
$options{'CONFIG'}{'ACTION'}{'GROUP'}="create,".
                                      "kill,".
                                      "user-basename,".
                                      "user-number,".
                                      "comment,".
                                      "password,".
                                      "valid-until,".
                                      "description,".
                                      "school";

# define which options deliver which object
$options{'CONFIG'}{'ONE_OF'}{'GROUP'}="extraclass";
#$options{'CONFIG'}{'SINGLE'}{'ACTION'}="delete-all-empty-classes,delete-all-teacheronly-classes,update-maildomain";
## define option that can modify which object
#$options{'CONFIG'}{'MAYBE'}{'GROUP'}="all,".
#                                     "teacherclass,".
#                                     "adminclass,".
#                                     "extraclass,";
## define more dependencies
#$options{'CONFIG'}{'DEPENDS'}{'gidnumber-migrate'}="create";
#$options{'CONFIG'}{'DEPENDS'}{'skip-school-creation'}="create";

my $testopt=GetOptions(\%options, 
                       "help|h",
                       "info|i",
                       "json|j+",
                       "verbose|v+",
                       "create",
                       "kill",
                       "extraclass=s",
                       "user-basename=s",
                       "user-number=i",
                       "comment=s",
                       "password=s",
                       "valid-until=s",
                       "description=s",
                       "school=s",
                      );    

my %sophomorix_result=&result_sophomorix_init("sophomorix-extraclass");
# Prüfen, ob Optionen erkannt wurden
&check_options($testopt,\%sophomorix_result,$options{'json'},\%options);

if (not defined $options{'school'}){
    $options{'school'}="---";
}


print Dumper (\%options);

# Reading Configuration
my ($ldap,$root_dse) = &AD_bind_admin(\@arguments,\%sophomorix_result,$options{'json'});
my $root_dns=&AD_dns_get($root_dse);
my %sophomorix_config=&config_sophomorix_read($ldap,$root_dse,\%sophomorix_result);
my ($smb_admin_pass)=&AD_get_passwd($DevelConf::sophomorix_file_admin,
                                     $DevelConf::secret_file_sophomorix_file_admin);

my $class_type=$sophomorix_config{'INI'}{'EXTRACLASS'}{'GROUP_TYPE'};


# --help
#if ($help==1) {
if (defined $options{'help'}) {
   # Scriptname ermitteln
   my @list = split(/\//,$0);
   my $scriptname = pop @list;
   # Befehlbeschreibung
   print('
sophomorix-extraclass creates and manages extraclasses

Options
  -h  / --help
  -v  / --verbose
  -vv / --verbose --verbose

  -i  / --info

Create an extraclass with a random password for each user:
  --create
  --extraclass <gruppenname>
  --user-basename <basename>
  --user-number <30>
  --valid-until <10.10.2020>

Optional:
  Common password for all users created (password must be complex enough):
    --password \'<password>\'
  Add a comment to all users:
    --comment "Für AK Linux"


For more changes and option use the command sophomorix-class:
  Show all extraclasses:
    # sophomorix-class -i --extraclass
  Show detailed info for a class:
    # sophomorix-class -i -c <extraclass>
  

Please see the sophomorix-extraclass(8) man pages for full documentation
');
   print "\n";
   exit;
}


############################################################
# config values
############################################################
my %convert_extension = qw(
     1     01
     2     02
     3     03
     4     04
     5     05
     6     06
     7     07
     8     08
     9     09
);



&result_sophomorix_check_exit(\%sophomorix_result,\%sophomorix_config,$options{'json'});
############################################################
# Start
############################################################
&log_script_start(\@arguments,\%sophomorix_result,\%sophomorix_config);


if (defined $options{'create'} and 
    defined $options{'extraclass'} and
    defined $options{'user-basename'} and
    defined $options{'user-number'} and
    defined $options{'valid-until'}
   ){

    #testing date ?????
    my ($until_day, $until_month, $until_year)=split(/\./, $options{'valid-until'});
    my $extraclass_enddate_epoch=timelocal(0, 0, 0, $until_day, $until_month-1, $until_year);
    if ($extraclass_enddate_epoch>=$sophomorix_config{'DATE'}{'LOCAL'}{'EPOCH'}){
    } else {
        print "\nvalid-until is NOT in the future!\n\n";
#        exit;
    }
    my $extraclass_enddate_AD=$until_year.$until_month.$until_day."000000".".0Z";

    my $group_token=&AD_get_name_tokened($options{'extraclass'},$options{'school'},$class_type);
    print "HERE create group $group_token $extraclass_enddate_AD \n";

    # weiter: gruppe anlegen, user anlegen

    &AD_school_create({ldap=>$ldap,
                       root_dse=>$root_dse,
                       root_dns=>$root_dns,
                       school=>$options{'school'},
                       smb_admin_pass=>$smb_admin_pass,
                       sophomorix_config=>\%sophomorix_config,
                       sophomorix_result=>\%sophomorix_result,
                     });
    &AD_group_create({ldap=>$ldap,
                      root_dse=>$root_dse,
                      root_dns=>$root_dns,
                      school=>$options{'school'},
                      group=>$group_token,
                      group_basename=>$options{'extraclass'},
                      description=>$sophomorix_config{'INI'}{'EXTRACLASS'}{'CLASS_DESCRIPTION'},
                      type=>$class_type,
                      status=>$sophomorix_config{'INI'}{'EXTRACLASS'}{'GROUP_STATUS'},
	              sub_ou=>"OU=".$options{'extraclass'}.",".$sophomorix_config{'INI'}{'OU'}{'AD_student_ou'},
                      joinable=>"FALSE",
                      hidden=>"FALSE",
                      smb_admin_pass=>$smb_admin_pass,
                      sophomorix_config=>\%sophomorix_config,
                      sophomorix_result=>\%sophomorix_result,
                    });



    my @password_chars=&get_passwd_charlist();

    my $file;
    if ($options{'school'} eq "---"){
        $file="students.csv";
    } else {
        $file=$options{'school'}.".students.csv";
    }

    my $comment;
    if (defined $options{'comment'}){
        $comment=$options{'comment'};
    } else {
        $comment="---";
    }
    
    for (my $i=1;$i<=$options{'user-number'};$i++){
        my $extension;
        if (exists $convert_extension{$i}){
            $extension=$convert_extension{$i};
        } else {
            $extension=$i;
        }
        my $username=$options{'user-basename'}.$extension;

        my $password;
	my $random_pwd;
	if (defined $options{'password'}){
            $random_pwd="FALSE";
            $password=$options{'password'};
	} else {
            $random_pwd="TRUE";
            my $pwd_length;
            if (exists $sophomorix_config{'FILES'}{'USER_FILE'}{$file}{'PWD_LENGTH'}){
                $pwd_length=$sophomorix_config{'FILES'}{'USER_FILE'}{$file}{'PWD_LENGTH'};
            } else {
                $pwd_length=33;
	    }

            $password=&get_plain_password(
                      $sophomorix_config{'INI'}{'EXTRACLASS'}{'USER_ROLE'},
                      $file,
                      $random_pwd, # TRUE/FALSE
                      $pwd_length, # length of random pwd
                      \%sophomorix_config,
                      @password_chars);

	}

        &AD_user_create({ldap=>$ldap,
                        root_dse => $root_dse, 
                        root_dns => $root_dns, 
                        user_count => $i,
                        max_user_count => $options{'user-number'},
                        login => $username,
                        group => $group_token,
                        group_basename => $options{'extraclass'},
                        firstname_ascii => $sophomorix_config{'INI'}{'EXTRACLASS'}{'USER_FIRSTNAME_ASCII'},
                        surname_ascii => $username,
                        firstname_utf8 => $sophomorix_config{'INI'}{'EXTRACLASS'}{'USER_FIRSTNAME_UTF8'},
                        surname_utf8 => $username,
                        birthdate => $sophomorix_config{'INI'}{'EXTRACLASS'}{'USER_BIRTHDATE'},
                        sophomorix_first_password => $password,
                        unid => "---",
                        role => $sophomorix_config{'INI'}{'EXTRACLASS'}{'USER_ROLE'},
                        type => $sophomorix_config{'INI'}{'EXTRACLASS'}{'GROUP_TYPE'},
                        school => $options{'school'},
                        tolerationdate => '---',
                        deactivationdate => $extraclass_enddate_AD,
                        status => $sophomorix_config{'INI'}{'EXTRACLASS'}{'USER_STATUS'},
                        file => $file,
                        smb_admin_pass=>$smb_admin_pass,
                        comment=>$comment,
                        json=>$options{'json'},
                        webui_permissions_calculated=>$sophomorix_config{'ROLES'}{$options{'school'}}{'student'}{'UI'}{'WEBUI_PERMISSIONS'},
                        sophomorix_config=>\%sophomorix_config,
                        sophomorix_result=>\%sophomorix_result,
                       });
    }
}


############################################################
# End
############################################################
&AD_unbind_admin($ldap);
&log_script_end(\@arguments,\%sophomorix_result,\%sophomorix_config,$options{'json'});







