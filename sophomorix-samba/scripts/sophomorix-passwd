#!/usr/bin/perl -w
# This script (sophomorix-passwd) is maintained by Rüdiger Beck
# It is Free Software (License GPLv3)
# If you find errors, contact the author
# jeffbeck@web.de  or  jeffbeck@linuxmuster.net

# modules
use strict;
use Getopt::Long;
Getopt::Long::Configure ("bundling");
use Sophomorix::SophomorixConfig;
use List::MoreUtils qw(uniq);
use Net::LDAP;
use JSON;
use Sophomorix::SophomorixBase qw(
                                 print_line
                                 print_title
                                 unlock_sophomorix
                                 lock_sophomorix
                                 log_script_start
                                 log_script_end
                                 log_script_exit
                                 backup_auk_file
                                 get_passwd_charlist
                                 get_plain_password
                                 create_plain_password
                                 check_options
                                 config_sophomorix_read
                                 result_sophomorix_init
                                 result_sophomorix_add
                                 result_sophomorix_check_exit
                                 result_sophomorix_print
                                 );
use Sophomorix::SophomorixSambaAD qw(
                                 AD_get_unicodepwd
                                 AD_set_unicodepwd
                                 AD_school_create
                                 AD_bind_admin
                                 AD_unbind_admin
                                 AD_user_create
                                 AD_user_update
                                 AD_group_create
                                 AD_group_addmember
                                 AD_group_update
                                 AD_get_schoolname
                                 AD_get_name_tokened
                                 AD_dn_fetch_multivalue
                                 AD_object_search
                                 AD_login_test
                                 AD_debug_logdump
                                 AD_dns_get
                                 AD_computer_update
                                 AD_get_user
                                    );

my @arguments = @ARGV;
my @users=();

# option vars
$Conf::log_level=1;
my $help=0;
my $info=0;
my $json=0;

my $school="";
my $test_login=0;
my $test_firstpassword=0;
my $set_firstpassword=0;

my $user="";
my $computer="";
my $clone_from_user="";
my $clone_to_user="";
my $classes="";
my $projects="";
my $student=0;
my $teacher=0;
my $rooms="";
my $ws=0;

my $password="";
my $configfile=0;
my $reset=0;
my $common=0;
my $random=0;
my $all_characters=0;

my $smb_pw_m_change=2;

my $shell="";
my $show_help=0;

my $interactive=0;
my $nofirstpassupdate=0;
my $char_num=0;

my $hide=0;
my $force=0;

# flag, if (1) user has specified ONE password for all (-p or --common)  , 
#    or if (2) password must be calculated for each user
my $password_given=0;

my $password_lehrer="";
my $password_other="";

my $info_line="";

my $show_password_charlist=0;


# Parsen der Optionen
my $testopt=GetOptions(
           "help|h" => \$help,
           "info|i" => \$info,
           "json|j+" => \$json,
           "verbose|v+" => \$Conf::log_level,
           "users|user|u=s" => \$user,
           "computer=s" => \$computer,
           "clone-from-user=s" => \$clone_from_user,
           "clone-to-user=s" => \$clone_to_user,
#           "class|classes|c=s" => \$classes,
#           "project|projects=s" => \$projects,
#           "student|students|s" => \$student,
#           "teacher|teachers|t" => \$teacher,
#           "room|rooms|r=s" => \$rooms,
#           "workstations|workstation|w" => \$ws,
           "password|passwd|pass=s" => \$password,
           "configfile" => \$configfile,
#           "reset" => \$reset,
           "common" => \$common,
           "random=i" => \$random,
           "interactive" => \$interactive,
#           "all-characters" => \$all_characters,
           "nofirstpassupdate" => \$nofirstpassupdate,
#           "plength=i" => \$char_num,
           "hide" => \$hide,
#           "samba-pwd-must-change!" => \$smb_pw_m_change,
#           "shell|loginshell=s" => \$shell,
#           "force" => \$force,
           "test-login" => \$test_login,
           "test-firstpassword" => \$test_firstpassword,
           "set-firstpassword" => \$set_firstpassword,
           "show-password-charlist" => \$show_password_charlist,
          );

my %sophomorix_result=&result_sophomorix_init("sophomorix-passwd");
# Prüfen, ob Optionen erkannt wurden, sonst Abbruch
&check_options($testopt,\%sophomorix_result,$json);

# Setting the filters

# school_filter
my $school_filter="";
if ($school ne ""){
    $school_filter="(sophomorixSchoolPrefix=$school)";
}

# user filter
my $user_filter="";
if ($user ne ""){
    $user_filter="(sAMAccountName=$user)";
}



# --help
if ($help==1) {
   # Scriptname ermitteln
   my @list = split(/\//,$0);
   my $scriptname = pop @list;
   # Befehlsbeschreibung
   print('
sophomorix-passwd modifies passwords in the AD

Options
  -h  / --help
  -v  / --verbose
  -vv / --verbose --verbose
  -i  / --info

Cloning a password-hash from one user to another:
  --clone-from-user <user1> --clone-to-user <user2>

Testing stuff:
  --show-password-charlist                     (show characters used to create random passwords)
  --set-firstpassword                          (ALL users: set passwords back to sophomorixFirstPassword attribute)
  --set-firstpassword -u <user1>,<user2>, ...  (set passwords back to sophomorixFirstPassword attribute)

  --test-login <user> --password <password-to-test>

  --test-firstpassword                         (ALL users: Test sophomorixFirstPassword as password)
  --test-firstpassword -u <user1>,<user2>, ... (Test sophomorixFirstPassword as password)

Options:
  --nofirstpassupdate (do not update AD attribute sophomorixFirstPassword)
  --hide (hide display of password in console output, when updating)

Create a password:
  --pass <password>, --password <password>
  --interactive              (query new password twice on console)
  --random <num>             (create random password of <num> characters for each user individually)
  --random <num> --common    (create random password of <num> characters once and apply it to all users)
  --configfile               (use *.school.conf of the user to create individual password)


Create userlist:
  -u <user1>,<user2>,...  /  --user  <user1>,<user2>,...
Create computerlist
  --computer <computer1>,<computer2>,...

Console examples:
  Interactively change password of a user (Always with --hide and --nofirstpassupdate)
    sophomorix-passwd --user schneima42 --interactive
  Create password according to *.school.conf settings
    sophomorix-passwd --configfile --user 

Webui examples:
  Change password for a user:
    sophomorix-passwd --user <user> --pass \'Muster!7\' --hide (--nofirstpassupdate) 
  Reset a password for a user from Schulkonsole:
    sophomorix-passwd --set-firstpassword -u <user1>,<user2>,...
  Test if the sophomorixFirstPassword is the valid password:
    sophomorix-passwd --test-firstpassword -u <user1>,<user2>,...


Todo:
Password change:
  --samba-pwd-must-change/nosamba-pwd-must-change
  --all-characters    (allow all characters in passwords,
                       only for developers, be careful!)
Create userlist:
  -s / --students
  -t / --teachers
  -w / --workstations (ExamAccouts)
  -c class1,class2,... /  --class class1,class2,... 
  --project project1,project2,... 
  -r room1,room2,...   /  --rooms room1,room2,...

Please see the sophomorix-passwd(8) man pages for full documentation
');
   print "\n";
   exit;
}


my ($ldap,$root_dse) = &AD_bind_admin(\@arguments,\%sophomorix_result,$json);
my $root_dns=&AD_dns_get($root_dse);
my %sophomorix_config=&config_sophomorix_read($ldap,$root_dse,\%sophomorix_result);



# --info --user <>
if ($info==1 and $user ne "") {
    my @users=split(",",$user);
    foreach my $user (@users){
        print "$user\n";
        my ($unicodepwd) = &AD_get_unicodepwd($user,\%sophomorix_config);
        print "   $user: $unicodepwd (unicodePwd)\n"
    }
    exit;
} elsif ($info==1){
    my $command="samba-tool domain passwordsettings show";
    print "\n# $command\n\n";
    system($command);
    print "\n";
    exit;
}



&result_sophomorix_check_exit(\%sophomorix_result,\%sophomorix_config,$json);
################################################################################
# Start
################################################################################
&log_script_start(\@arguments,\%sophomorix_result,\%sophomorix_config);

############################################################
# --clone-from-user <user1> --clone-to-user <user2>
if ( ($clone_from_user ne "" and $clone_to_user eq "") or
     ($clone_to_user ne "" and $clone_from_user eq "") ){
    print "\nERROR: --clone-from-user <user1> --clone-to-user <user2>\n\n";
    exit 88;
} elsif ($clone_from_user ne "" and $clone_to_user ne ""){
    &print_title("Cloning password-hash of $clone_from_user to $clone_to_user");
    my $unicodepwd= &AD_get_unicodepwd($clone_from_user,\%sophomorix_config);
    &AD_set_unicodepwd($clone_to_user,$unicodepwd,\%sophomorix_config);
    &log_script_end(\@arguments,\%sophomorix_result,\%sophomorix_config,$json);
}



############################################################
# --test-login
if ($test_login==1 and $password ne ""){
    my $filter="( &(objectclass=user) (cn=*) (sophomorixRole=*) $school_filter $user_filter)";
    my $mesg = $ldap->search(
                      base   => $root_dse,
                      scope => 'sub',
                      filter => $filter,
                      attr => ['cn']
                            );
    &AD_debug_logdump($mesg,2,(caller(0))[3]);
    my $max = $mesg->count;
    for( my $index = 0 ; $index < $max ; $index++) {
        my $entry = $mesg->entry($index);
        my $dn=$entry->dn();
        my $sam_account=$entry->get_value ('sAMAccountName');
        print "* $sam_account: $dn\n";
        my ($res,$testpassword)=&AD_login_test($ldap,$root_dse,$dn,$password);
        if ($res==0){
            print "   * Login with password $password OK!\n";
        } else {
            print "   * ERROR logging in with password $password: $res\n";
        }
    }
    &log_script_end(\@arguments,\%sophomorix_result,\%sophomorix_config,$json);
}



############################################################
# --test-firstpassword
if ($test_firstpassword==1){
    my $count_tested=0;
    my $count_success=0;
    my $count_fail=0;
    my $count_nofirstpass=0;
    my @success=();
    my @fail=();
    my @nofirst=();
    print "Testing if firstpassword works in the following Accounts\n";
    # search for all users
    my $filter="( &(objectclass=user) (cn=*) (sophomorixRole=*) $school_filter $user_filter)"; 
    if($Conf::log_level>=2){
        print "Filter to find users: $filter\n";
    }
    my $mesg = $ldap->search(
                      base   => $root_dse,
                      scope => 'sub',
                      filter => $filter,
                      attr => ['cn']
                            );
    &AD_debug_logdump($mesg,2,(caller(0))[3]);
    my $max = $mesg->count;
    for( my $index = 0 ; $index < $max ; $index++) {
        my $entry = $mesg->entry($index);
        my $dn=$entry->dn();
        my $sam_account=$entry->get_value ('sAMAccountName');
        $count_tested++;
        print "* $count_tested) $sam_account: $dn\n";
        my ($res,$testpassword)=&AD_login_test($ldap,$root_dse,$dn,$password);
        if ($res==0){
            print "   * Login with sophomorixFirstPassword $testpassword OK!\n";
            $count_success++;
            push @success, "OK: $dn\n";
        } elsif ( $res==-1) {
            print "   * No firstpassword found!\n";
            $count_nofirstpass++;
            push @nofirst, "No Password: $dn\n";
        } else {
            print "   * ERROR logging in with sophomorixFirstPassword $testpassword: $res\n";
            $count_fail++;
            push @fail, "Failed: $dn\n";
        }
    }
    &print_title("Test result:");
    if($Conf::log_level>=2){
        @success = sort @success;
	@fail = sort @fail;
	@nofirst = sort @nofirst;
        foreach my $line (@success,@fail,@nofirst){
            print "$line";
        } 
	&print_line;
    }
    print "$count_tested tested:  $count_success OK,",
          "  $count_fail Failed Logins,",
          " $count_nofirstpass Without Firstpassword Attribute\n";
    &log_script_end(\@arguments,\%sophomorix_result,\%sophomorix_config,$json);
}



############################################################
# --set-firstpassword
if ($set_firstpassword==1){
    # search for all users
    my $filter="( &(objectclass=user) (cn=*) (sophomorixRole=*) $school_filter $user_filter)"; 
    if($Conf::log_level>=2){
        print "Filter to find users: $filter\n";
    }
    my $mesg = $ldap->search(
                      base   => $root_dse,
                      scope => 'sub',
                      filter => $filter,
                      attr => ['sAMAccountName','cn','sophomorixFirstPassword']
                            );
    &AD_debug_logdump($mesg,2,(caller(0))[3]);
    my $max = $mesg->count;
    for( my $index = 0 ; $index < $max ; $index++) {
        my $entry = $mesg->entry($index);
        my $dn=$entry->dn();
        my $sam_account=$entry->get_value ('sAMAccountName');
        my $firstpassword=$entry->get_value ('sophomorixFirstPassword');
        my $user_count=$index+1;
        &AD_user_update({ldap=>$ldap,
                     root_dse=>$root_dse,
                     dn=>$dn,
                     user=>$sam_account,
                     user_count=>$user_count,
                     max_user_count=>"-",
                     hide_pwd=>$hide,
#                     firstpassword=>$firstpassword,
                     sophomorix_first_password=>$firstpassword,
                     json=>$json,
                     sophomorix_config=>\%sophomorix_config,
                     sophomorix_result=>\%sophomorix_result,
                   });
    }
    &log_script_end(\@arguments,\%sophomorix_result,\%sophomorix_config,$json);
}



############################################################
# creating passwords
############################################################
my @password_chars=();



# --random
if ($random!=0){
    @password_chars=&get_passwd_charlist();
    if ($random<$sophomorix_config{'samba'}{'domain_passwordsettings'}{'Minimum_password_length'}){
        print "\nERROR: Random password length $random is shorter than required minimun of ",
              "$sophomorix_config{'samba'}{'domain_passwordsettings'}{'Minimum_password_length'}\n\n";
        exit;
    }
}



# --configfile
if ($configfile==1){
    @password_chars=&get_passwd_charlist();
}



# --common
if ($common==1){
    @password_chars=&get_passwd_charlist();
    $password=&create_plain_password($random,@password_chars);
}



# --show-password-charlist
if ($show_password_charlist==1){
    @password_chars=&get_passwd_charlist();
    print "Valid characters in a randomly generated password:\n";
    foreach my $char (@password_chars){
        print " $char";
    }
    print "\n";
    exit;
}



############################################################
# setting passwords from userlist
############################################################
my @userlist=();
# --user
if ($user ne ""){
    @userlist=split(",",$user);
}
my $userlist_count=$#userlist+1;

# set a password from userlist
if ($userlist_count>0 and ($password ne "" or 
                           $random!=0 or
                           $configfile==1)){
    my $user_count=0;
    foreach my $userlist_user (@userlist){
	$user_count++;
	my $filter="( &(objectclass=user) (sAMAccountName=$userlist_user) )"; 
        my $mesg = $ldap->search(
                          base   => $root_dse,
                          scope => 'sub',
                          filter => $filter,
                          attr => ['sAMAccountName','cn','dn','sophomorixFirstPassword']
                                );

        &AD_debug_logdump($mesg,2,(caller(0))[3]);
        my $max = $mesg->count;
	if ($max==1){
            # create pasword for each user
            if ($random!=0 and $common==0){
                $password=&create_plain_password($random,@password_chars);
            } elsif ($configfile==1){
                my ($firstname_utf8_AD,$lastname_utf8_AD,$adminclass_AD,$existing_AD,$exammode_AD,$role_AD,
                    $home_directory_AD,$user_account_control_AD,$toleration_date_AD,$deactivation_date_AD,
                    $school_AD,$status_AD,$firstpassword_AD,$unid_AD,$firstname_ASCII_AD,$lastname_ASCII_AD,
                    $firstname_initial_AD,$lastname_initial_AD,$user_token_AD,$file_AD)=
                    &AD_get_user({ldap=>$ldap,
                                  root_dse=>$root_dse,
                                  root_dns=>$root_dns,
                                  user=>$userlist_user,
                                });
                    $password=&get_plain_password(
                        $role_AD,
                        $file_AD,
                        $sophomorix_config{'FILES'}{'USER_FILE'}{$file_AD}{'RANDOM_PWD'}, # yes|no
                        $sophomorix_config{'FILES'}{'USER_FILE'}{$file_AD}{'PWD_LENGTH'}, # length of random pwd
                        \%sophomorix_config,
                        @password_chars);
            }
            my $entry = $mesg->entry(0);
            my $dn=$entry->dn();
            if ($nofirstpassupdate==1){
                &AD_user_update({ldap=>$ldap,
                                 root_dse=>$root_dse,
                                 dn=>$dn,
                                 user=>$userlist_user,
                                 user_count=>$user_count,
                                 max_user_count=>$userlist_count,
                                 hide_pwd=>$hide,
                                 sophomorix_first_password=>$password,
                                 # firstpassword=>$password, # not updated
                                 json=>$json,
                                 sophomorix_config=>\%sophomorix_config,
                                 sophomorix_result=>\%sophomorix_result,
			        });
	    } else {
                &AD_user_update({ldap=>$ldap,
                                 root_dse=>$root_dse,
                                 dn=>$dn,
                                 user=>$userlist_user,
                                 user_count=>$user_count,
                                 max_user_count=>$userlist_count,
                                 hide_pwd=>$hide,
                                 sophomorix_first_password=>$password,
                                 firstpassword=>$password,
                                 json=>$json,
                                 sophomorix_config=>\%sophomorix_config,
                                 sophomorix_result=>\%sophomorix_result,
			        });
	    }
	} else {
            print "\n";
            print "WARNING: Skipping user \"$userlist_user\" ($max users found instead of 1)\n\n";
	}
    }
} elsif ($password eq "" and $userlist_count>0) {
    print "\n";
    print "WARNING: No password given, but $userlist_count users in userlist\n";
    print "         I do not know what password you want!\n\n";        
}



############################################################
# setting passwords from computerlist
############################################################
my @computerlist=();
# --computer
if ($computer ne ""){
    @computerlist=split(",",$computer);
}
my $computerlist_count=$#computerlist+1;

# set a password from computerlist
if ($password ne "" and $computerlist_count>0){
    my $computer_count=0;
    foreach my $computerlist_user (@computerlist){
	$computer_count++;
        $computerlist_user=~tr/a-z/A-Z/;
        $computerlist_user=&Sophomorix::SophomorixBase::append_dollar($computerlist_user);
        my %update_computer=();
        $update_computer{$computerlist_user}{'REPLACE'}{'unicodePwd'}=$password;
        $update_computer{$computerlist_user}{'COUNT'}=1;
	my $filter="( &(objectclass=user) (sAMAccountName=$computerlist_user) )";
        my $mesg = $ldap->search(
                          base   => $root_dse,
                          scope => 'sub',
                          filter => $filter,
                          attr => ['sAMAccountName','cn','dn','sophomorixFirstPassword']
                                );
        &AD_debug_logdump($mesg,2,(caller(0))[3]);
        my $max = $mesg->count;
	if ($max==1){
            my $entry = $mesg->entry(0);
            my $dn=$entry->dn();
        &AD_computer_update({ldap=>$ldap,
                             root_dse=>$root_dse,
                             computer=>$computerlist_user,
                             computer_count=>$computer_count,
                             attrs_count=>$update_computer{$computerlist_user}{'COUNT'},
                             replace=>\%update_computer,
                             sophomorix_first_password=>$password,
                             hide_pwd=>$hide,
                             max_computer_count=>$computerlist_count,
                             json=>$json,
                             sophomorix_config=>\%sophomorix_config,
                             sophomorix_result=>\%sophomorix_result,
                           });
	} else {
            print "\nERROR: $max users $computerlist_user found instead of 1\n\n";
	}
    }
} elsif ($password eq "" and $computerlist_count>0) {
    print "\n";
    print "WARNING: No password given, but $computerlist_count users in computerlist\n";
    print "         I do not know what password you want!\n\n";
}





############################################################
# Setting passwords interacive
############################################################
if ($interactive==1){
    if ($userlist_count==1){
        # ask for password
        use Term::ReadKey;
        ReadMode('noecho');
        # ask once
        print "New password : ";
        my $password_1 = ReadLine(0);
        print "\n";
        chomp($password_1);

        # ask again
        print "Retype new password : ";
        my $password_2 = ReadLine(0);
        print "\n";
        chomp($password_2);
        # reset to echo
        ReadMode('normal');

        # Look if they match
        if ($password_1 eq $password_2){
            print "New passwords match!\n";
 	    my $filter="( &(objectclass=user) (sAMAccountName=$user) )"; 
            my $mesg = $ldap->search(
                              base   => $root_dse,
                              scope => 'sub',
                              filter => $filter,
                              attr => ['sAMAccountName','dn']
                                     );
            &AD_debug_logdump($mesg,2,(caller(0))[3]);
            my $max = $mesg->count;
	    if ($max==1){
                my $entry = $mesg->entry(0);
                my $dn=$entry->dn();
                &AD_user_update({ldap=>$ldap,
                                 root_dse=>$root_dse,
                                 dn=>$dn,
                                 user=>$user,
                                 user_count=>"1",
                                 max_user_count=>"1",
                                 hide_pwd=>"1",
                                 sophomorix_first_password=>$password_1,
                                 # firstpassword=>$password, # not updated
                                 json=>$json,
                                 sophomorix_config=>\%sophomorix_config,
                                 sophomorix_result=>\%sophomorix_result,
                                });
	    }
            exit 0;
         } else {
             print "\nERROR: New passwords don't match!\n\n";
             exit 88;
         }
     } else {
         print "\nERROR: I can set password interactively only for one user\n";
         print "Unable to change password\n\n";
         exit 88;
     }

}



&log_script_end(\@arguments,\%sophomorix_result,\%sophomorix_config,$json);



# sub validate_password {
#     my ($password) = @_;
#     # - and $ escaped
#     if ($password=~/[^0-9A-Za-z@!\$%&?_\-:;.,]/ and $all_characters==0){
#         print "New password contains unallowed characters!\n";
#         &nscd_start();
#         exit 5;
#     }
# }
